plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'org.sonarqube'
}
apply from: '../jacoco.gradle'

android {
    namespace 'com.likeminds.feedsx'
    compileSdkVersion versions.compile_sdk

    defaultConfig {
        minSdk versions.min_sdk
        targetSdk versions.target_sdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }
    sonarqube {
        properties {
            property "sonar.projectName", "Android-Feed-UI"
            property "sonar.projectKey", "Android-Feed-UI"
            property "sonar.sources", "src"
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.tests", ["src/test/java"]
            property "sonar.test.inclusions", "**/*Test*/**"
            property "sonar.exclusions", '**/.gradle/**,' +
                    "**/androidTest/**," +
                    "**/R.class," +
                    "**/BuildConfig.*," +
                    "**/Manifest*.*," +
                    "**/android/databinding/*," +
                    "**/androidx/databinding/*," +
                    "**/*MapperImpl*.*," +
                    "**/BuildConfig.*," +
                    "**/Manifest*.*," +
                    "**/google-services.json"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.junit.reportPaths", "**/test-results/**/*.xml"
            property "sonar.coverage.jacoco.xmlReportPaths", "**/jacocoTestReport/jacocoTestReport.xml"
            property "sonar.coverage.exclusions", "${coverageExclusions.join(', ')}"
        }
    }
}

apply from: "dependencies.gradle"