plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'org.sonarqube'
}
apply from: '../jacoco.gradle'

android {
    namespace 'com.likeminds.feedsampleapp'
    compileSdk versions.compile_sdk

    defaultConfig {
        applicationId "com.likeminds.feedsampleapp"
        minSdk versions.min_sdk
        targetSdk versions.target_sdk
        versionCode versions.app_version_code
        versionName "$versions.app_version_major.$versions.app_version_minor.$versions.app_version_patch"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled = true
        }
    }
    buildFeatures {
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    sonarqube {
        properties {
            property "sonar.projectName", "Android-Feed-UI"
            property "sonar.projectKey", "Android-Feed-UI"
            property "sonar.sources", "src"
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.binaries", "build/intermediates/classes/debug"
            property "sonar.java.binaries", "build/intermediates/classes/debug"
            property "sonar.tests", ["src/test/java"]
            property "sonar.test.inclusions", "**/*Test*/**"
            property "sonar.exclusions", '**/.gradle/**,' +
                    "**/androidTest/**," +
                    "**/R.class," +
                    "**/BuildConfig.*," +
                    "**/Manifest*.*," +
                    "**/android/databinding/*," +
                    "**/androidx/databinding/*," +
                    "**/*MapperImpl*.*," +
                    "**/BuildConfig.*," +
                    "**/Manifest*.*," +
                    "**/google-services.json"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.junit.reportPaths", "**/test-results/**/*.xml"
            property "sonar.coverage.jacoco.xmlReportPaths", "**/jacocoTestReport/jacocoTestReport.xml"
            property "sonar.coverage.exclusions", "${coverageExclusions.join(', ')}"
        }
    }
}

apply from: "dependencies.gradle"